module libdsldi/sel/result-reporting

signature
  constructors
      // name, followed by a list of expression
      
      // source-filename, target-filename
      Success       : ID * ID -> InstrumentationResult
      
      // source-filename
      Failure      : ID * ID -> InstrumentationResult

imports
    libdsldi/logging/logging // logging
    libdsldi/utils
    libdsldi/sel/program-metadata
    
imports
    libdsldi/sel/options
    libdsldi/sel/dsl-io

imports
  libstratego-lib
  libstratego-sglr
  libstratego-gpp
      
strategies
    isSuccess :
        s@Success(_,_) -> s
    isFailure :
        f@Failure(_,_) -> f

    getFilenamesWithSuccess =
        filter(isSuccess) ; map(\Success(input, output) -> input\)
    getFilenamesWithFailure =
        filter(isFailure) ; map(\Failure(message, location) -> location\)
        
    result-item-to-row-string :
        Failure(message, location) -> <separate-by(|"\t"); concat-strings> [location, message]
    result-item-to-row-string :
        Success(input, output) -> <separate-by(|"\t"); concat-strings> [input, output]
        
    /**
     * Write instrumentation statistics to STDOUT
     * Returns a list of terms, each item in de list is Success or Failure term and corresponds to an instrumented file.
     */
    report-instrumentation =
        result := <GET-INSTRUMENTATION-SUMMARY>
        ; log-info-msg(|"Instrumentation summary:")
        ; suc-count := <filter(isSuccess); length> result
        ; fail-count := <filter(isFailure); length> result
        ; <log-info(|"instrumented file count: ")> suc-count
        ; <log-info(|"instrumentation failed count: ")> fail-count
        ; if get-report-failures-location then
              get-report-failures-location
              ; log-info(|"Creating report with failed files:")
              ; !result
              ; create-result-report(isFailure, result-item-to-row-string|<get-report-failures-location>)
          end
        ; if get-report-success-location then
              get-report-success-location
              ; log-info(|"Creating report with succeeded files...")
              ; !result
              ; create-result-report(isSuccess, result-item-to-row-string|<get-report-success-location>)
          end
        //; !("foo.aterm", "contents")
        ; if get-runtime-statistics-location then
              try(make-file-based-stats)
              ; try(make-grouped-stats)
          end
        ; if get-config-show-runtime-stats then
              report-all-workunit-stats
          end
        ; !result
          
    make-file-based-stats = 
      filename := <get-runtime-statistics-location>
      ; file-stats-location := <concat-strings> [filename, ".file.stats"]
      ; log-info(|"Writing file-based runtime stats to ")
      ; handle := <fopen> (file-stats-location, "w")
      ; finally(id
            // write header
            ; get-all-stat-value-keys
            ; <concat> [["filename"], <id>]
            ; separate-by(|"\t")
            ; concat-strings
            ; <fputs> (<id>, handle)
            ; <fputs> ("\n", handle)
            // write contents
            ; visit-all-workunit-stats(\row -> row where <fputs> (row, handle); <fputs> ("\n", handle) \)
            , <fclose> handle
        )
        
    make-grouped-stats = 
      filename := <get-runtime-statistics-location>
      ; group-stats-location := <concat-strings> [filename, ".group.stats"]
      ; log-info(|"Writing grouped runtime stats to ")
      ; handle := <fopen> (group-stats-location, "w")
      ; finally(id
            ; get-all-stat-value-keys
            ; map(
                  \stat-value-key -> stat-value-key
                    where
                        group-by-stat-key
                        ; try(
	                        (list-accum(add) <+ !0)
	                        ; write-to-string
	                        ; ![stat-value-key, <id>]
	                        ; separate-by(|"\t")
	                        ; concat-strings
	                        ; <fputs> (<id>, handle)
	                        ; <fputs> ("\n", handle)
                        )
                  \
              )
            , <fclose> handle
        )
        
    create-result-report(result-filter, result-item-to-row-string|report-location) = ?result
        ;filter(result-filter)
        ;map(result-item-to-row-string)
        ; separate-by(|"\n")
        ; concat-strings => contents
        ; write-to-file(|report-location, contents)
          <+ <err(|"Could not write report to:")> report-location

    
    failed-extract(|extract-name) =
        ?program-fragment-aterm
        ; <err(|"Failed to execute extract strategy: ")> extract-name
        ; <log-debug-depth(err(|"... on term:"))> program-fragment-aterm
        ; <log-vomit(|"... on term: ")> program-fragment-aterm

    failed-generate(|generate-name) =
        ?program-fragment-aterm
        ; <err(|"Failed to execute generate strategy: ")> generate-name
        ; <log-debug-depth(err(|"... on term:"))> program-fragment-aterm
        ; <log-vomit(|"... on term: ")> program-fragment-aterm
          
    // a list of terms, each item in de list is Success or Failure term and corresponds to an instrumented file.
    SET-INSTRUMENTATION-SUMMARY = ?result
        ; rules(GET-INSTRUMENTATION-SUMMARY: _ -> result)

    // the total number of files that will be instrumented
    SET-TOTAL-WORKUNITS = ?count
        ;rules(GET-TOTAL-WORKUNITS: _ -> count)

    // the progress, should be between 0 and GET-TOTAL-WORKUNITS
    SET-WORKUNIT-PROGRESS = ?progress
        ;rules(GET-WORKUNIT-PROGRESS: _ -> progress)
         
    next-workunit = 
        GET-WORKUNIT-PROGRESS
        ; inc
        ; SET-WORKUNIT-PROGRESS
          
    workunit-progress-as-string =
        <concat-strings> [
            <GET-WORKUNIT-PROGRESS; int-to-string>,
            "/",
            <GET-TOTAL-WORKUNITS; int-to-string>
        ]
        
strategies

  /**
   * @param s The strategy for which we'll do the timing
   * @param msg A message to describe the strategy in the logs
   * @param level The logging level of the statistics
   */
  dsldi-log-timed(s|msg,severity : Severity) =
    ?current
    ; level := <verbose-level> severity
    ; if <geq>(<get-config> "--statistics", level) then
        where(times => starttime)
        ; !current; s
        ; where(
            <diff-times>(<times>,starttime)
            ; <concat-strings>[ <align-left>(' ', msg, 27)
                        , " : [user/system] = ["
                        , <self-children-user-time; ticks-to-seconds ; real-to-string(|2)>
                        , "s/"
                        , <self-children-sys-time; ticks-to-seconds ; real-to-string(|2)>
                        , "s]"
                        ]
            ; log(|Info(),<id>)
        )
    else
      !current; s
    end
    
strategies
    
    /**
     * Measure duration of Strategy s which is part of a workunit.
     * The duration can be retreived by calling <GET-SUBWORKUNIT-STAT> stat-key 
     */
    profile-subworkunit-stat-duration(s|stat-key) = 
    	?current
    	; where(times => starttime)
    	; !current; s
    	; where(
    	      <diff-times>(<times>,starttime) => duration
    	      ; <self-children-user-time; ticks-to-seconds /*; real-to-string(|2)*/> duration => user-time
    	      ; <self-children-sys-time; ticks-to-seconds /*; real-to-string(|2)*/> duration => sys-time
    	      //; <SET-SUBWORKUNIT-STAT> (stat-key, (user-time, sys-time)) // record user and sys time
    	      ; <SET-SUBWORKUNIT-STAT> (stat-key, user-time) // record user time
          )
    
    /**
     * Get all subworkunit statistics defined for the current workunit scope.
     * The subworkunit stats are stored as dynamic rules within the "workunit" dr-scope.
     * Get them before the dr-scope ends.
     * List<(stat-key<String>, stat)>
     */
    get-all-workunit-stats =
    	all-keys-GET-SUBWORKUNIT-STAT
    	; map(\stat-key -> (stat-key, <GET-SUBWORKUNIT-STAT>)\) => stats-list
    
    SET-SUBWORKUNIT-STAT = ?(stat-key, stat-value)
    ; rules(GET-SUBWORKUNIT-STAT: stat-key -> stat-value)
    
    SET-WORKUNIT-STATS = ?(workunit-key, stats)
    ; rules(GET-WORKUNIT-STATS: workunit-key -> stats)
    
    inc-subworkunit-stat(|stat-key) = 
        if <GET-SUBWORKUNIT-STAT> stat-key then
            <GET-SUBWORKUNIT-STAT> stat-key
        else
            !0
        end
        ; inc
        ; <SET-SUBWORKUNIT-STAT> (stat-key, <id>)
    
    // print line per workunit (i.e. instrumented file)
    visit-all-workunit-stats(write-row) = id
        ; all-keys-GET-WORKUNIT-STATS
        ; map(workunit-stat-to-row-string(get-all-stat-value-keys); write-row)
    
    get-all-stat-value-keys = id
        ; !["Enter-event-count", "Exit-event-count", "Step-event-count"] => event-counters
        ; !["program-term-size"] => size-counters
        ; !["parse-time", "instrumentation-time", "write-time", "file-total-time"] => timings
        ; <concat> [event-counters, size-counters, timings]
    
    // write filename, enter/exit/step-count, total-duration, parse-duration, instr-duration, write-duration
    workunit-stat-to-row-string(get-stat-value-keys) = ?filename-key
        ; get-stat-value-keys // List<String>
        ; map(!(<id>, <!filename-key; GET-WORKUNIT-STATS>); lookup <+ !0) // if stat-value-key is not found return 0 as default value
        ; map(write-to-string)
        ; <concat> [[filename-key], <id>]
        ; separate-by(|"\t")
        ; concat-strings
          
    subworkunit-stat-value-to-string = 
        ?(stat-key, stat-value)
        ; if <is-string> stat-value then
              !stat-value
          else 
              <write-to-string> stat-value
          end
          
    report-all-workunit-stats =
        log-info-msg(|"report-all-workunit-stats...") 
        ; all-keys-GET-WORKUNIT-STATS
        ; map(report-workunit-stat)
        ; stat-value-keys := <get-all-stat-value-keys>
        ; prefix-width := <map(string-length); list-max> stat-value-keys
        ; <map(try(report-subworkunit-total-on-count(|prefix-width)))> stat-value-keys
        // difference between counter and time => count contains a integer and time contains a tuple of doubles
        //; event-counters-prefix-width := <map(string-length); list-max> event-counters
        //; timings-prefix-width := <map(string-length); list-max> timings
        //; <map(try(report-subworkunit-total-on-count(|event-counters-prefix-width)))> event-counters 
        //; <map(try(report-subworkunit-total-on-time(|timings-prefix-width)))> timings

    // Values for stat-key should be integers
    report-subworkunit-total-on-count(|prefix-width) = ?stat-key
        ; log-info(|"reporting statistics for ")
        ; <group-by-stat-key; list-accum(add); write-to-string> stat-key => total-count
        ; prefix-msg := <concat-strings> ["Total ", stat-key, ": "]
        ; log-info-msg(|<concat-strings> [
              <align-left>(' ', prefix-msg, <add> (prefix-width, 8))
              , total-count
              ])
    
    report-subworkunit-total-on-time(|prefix-width) = ?stat-key
        ; group-by-stat-key; unzip 
        ; where(Fst; list-accum(add); real-to-string(|2) => user-time)
        ; where(Snd; list-accum(add); real-to-string(|2) => sys-time)
        ; prefix-msg := <concat-strings> ["Total ", stat-key, ": "]
        ; log-info-msg(|<concat-strings> [
              <align-left>(' ', prefix-msg, <add> (prefix-width, 8)),
              user-time,
              "s/",
              sys-time,
              "s"
              ])

          
    report-workunit-stat = ?key
        ; log-info-msg(|<concat-strings> ["Workunit statistics for ", key]) 
        ; GET-WORKUNIT-STATS
        ; where(map(Fst; string-length); list-max; ?prefix-width)
        ; map(report-subworkunit-stat(|prefix-width))
         
    report-subworkunit-stat(|prefix-width) = ?(stat-key, stat-value)
        //; <log-info(|" stat-key: ")> stat-key
        //; <log-info(|" stat-val: ")> stat-value
        ; if <is-string> stat-value then
              !stat-value
          else 
              <write-to-string> stat-value
          end
        ; ?stat-value-string
        ; prefix-msg := <concat-strings>[ "subworkunit ", stat-key, ": "]
      ; <concat-strings>[ <align-left>(' ', prefix-msg, <add> (prefix-width, 14))
                        , stat-value-string
                        ]
        ; ?log-line
        ; log-info-msg(|log-line)

    // returns List<Tuple<Real, Real>>
    group-by-stat-key = ?stat-key
        ; all-keys-GET-WORKUNIT-STATS
        ; map(GET-WORKUNIT-STATS; !(stat-key, <id>); lookup <+ !None()); remove-all(?None())

